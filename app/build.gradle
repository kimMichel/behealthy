plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

configurations {
    ktlint
}

android {
    namespace 'com.example.behealthy'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.behealthy"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    ktlint "com.pinterest:ktlint:0.48.1"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.14.0'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Security
    implementation 'androidx.security:security-crypto:1.1.0-alpha04'

    // Koin
    def koin_android_version = "3.3.0"
    implementation "io.insert-koin:koin-android:$koin_android_version"

    // Navigation
    def nav_version = "2.5.3"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style"
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

preBuild.dependsOn "ktlint"

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}